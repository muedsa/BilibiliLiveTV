plugins {
    id 'com.android.application'
}

Properties localProps = new Properties()
File localPropsFile = project.rootProject.file('local.properties')
if(localPropsFile.exists() && localPropsFile.canRead()){
    localProps.load(localPropsFile.newDataInputStream())
}

android {
    signingConfigs {
        release {
            if(localProps.containsKey('bltv.signingConfig.storeFile')) {
                storeFile file(localProps.getProperty('bltv.signingConfig.storeFile'))
                storePassword localProps.getProperty('bltv.signingConfig.storePassword')
                keyPassword localProps.getProperty('bltv.signingConfig.keyPassword')
                keyAlias localProps.getProperty('bltv.signingConfig.keyAlias')
            }else{
                storeFile signingConfigs.debug.storeFile
                storePassword signingConfigs.debug.storePassword
                keyPassword signingConfigs.debug.keyPassword
                keyAlias signingConfigs.debug.keyAlias
            }
        }
    }
    compileSdk 34

    defaultConfig {
        applicationId "com.muedsa.bilibililivetv"
        minSdk 26
        targetSdk 34
        versionCode 39
        versionName "1.9.1"
    }

    buildTypes {

        debug {
            minifyEnabled false
            proguardFiles 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    buildFeatures {
        viewBinding true
    }
    namespace 'com.muedsa.bilibililivetv'
}

dependencies {
    def leanback_version = "1.2.0-alpha02"
    def glide_version = "4.15.1"
    def exoplayer_version = "2.19.0"
    def rxjava3_version = "3.1.6"
    def rxjava3_android_version = "3.0.2"
    def room_version = "2.5.2"

    implementation project(path: ':BilibiliLiveApiClient')

    implementation 'androidx.preference:preference:1.2.0'

    implementation 'androidx.tvprovider:tvprovider:1.1.0-alpha01'
    implementation "androidx.leanback:leanback:$leanback_version"
    implementation "androidx.leanback:leanback-preference:$leanback_version"

    implementation ("com.github.bumptech.glide:glide:$glide_version") {
        exclude group: "com.android.support"
    }
    annotationProcessor "com.github.bumptech.glide:compiler:$glide_version"
    implementation 'jp.wasabeef:glide-transformations:4.3.0'

    implementation "com.google.android.exoplayer:exoplayer-core:$exoplayer_version"
    //implementation "com.google.android.exoplayer:exoplayer-dash:$exoplayer_version"
    implementation "com.google.android.exoplayer:extension-leanback:$exoplayer_version"
    implementation "com.google.android.exoplayer:exoplayer-ui:$exoplayer_version"

    implementation project(path: ':repository:DanmakuFlameMaster')
    implementation 'com.github.ctiao:ndkbitmap-armv7a:0.9.24'
    implementation 'com.github.ctiao:ndkbitmap-armv5:0.9.24'
    implementation 'com.github.ctiao:ndkbitmap-x86:0.9.24'

    implementation "io.reactivex.rxjava3:rxjava:$rxjava3_version"
    implementation "io.reactivex.rxjava3:rxandroid:$rxjava3_android_version"

    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-rxjava3:$room_version"
    testImplementation "androidx.room:room-testing:$room_version"

    implementation 'com.google.zxing:core:3.5.1'
}