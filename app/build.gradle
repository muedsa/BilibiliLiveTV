plugins {
    id 'com.android.application'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'androidx.room'
}

Properties localProps = new Properties()
File localPropsFile = project.rootProject.file('local.properties')
if (localPropsFile.exists() && localPropsFile.canRead()) {
    localProps.load(localPropsFile.newDataInputStream())
}

android {
    namespace 'com.muedsa.bilibililivetv'

    compileSdk 35

    defaultConfig {
        applicationId "com.muedsa.bilibililivetv"
        minSdk 26
        targetSdk 35
        versionCode 56
        versionName "1.9.16"
        setProperty("archivesBaseName", "BilibiliLiveTV")
    }

    signingConfigs {
        release {
            if (localProps.containsKey('bltv.signingConfig.storeFile')) {
                storeFile file(localProps.getProperty('bltv.signingConfig.storeFile'))
                storePassword localProps.getProperty('bltv.signingConfig.storePassword')
                keyPassword localProps.getProperty('bltv.signingConfig.keyPassword')
                keyAlias localProps.getProperty('bltv.signingConfig.keyAlias')
            } else {
                storeFile signingConfigs.debug.storeFile
                storePassword signingConfigs.debug.storePassword
                keyPassword signingConfigs.debug.keyPassword
                keyAlias signingConfigs.debug.keyAlias
            }
        }
    }

    buildTypes {

        debug {
            applicationIdSuffix ".debug"
            minifyEnabled false
            proguardFiles 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    buildFeatures {
        viewBinding true
    }

    room {
        schemaDirectory "$projectDir/schemas"
    }
}

dependencies {
    def leanback_version = "1.2.0-alpha04"
    def glide_version = "4.16.0"
    def media3_version = "1.4.1"
    def rxjava3_version = "3.1.9"
    def rxjava3_android_version = "3.0.2"
    def room_version = "2.6.1"

    implementation project(path: ':BilibiliLiveApiClient')

    implementation 'androidx.preference:preference:1.2.1'

    implementation 'androidx.tvprovider:tvprovider:1.1.0-alpha01'
    implementation "androidx.leanback:leanback:$leanback_version"
    implementation "androidx.leanback:leanback-preference:$leanback_version"

    implementation("com.github.bumptech.glide:glide:$glide_version") {
        exclude group: "com.android.support"
    }
    annotationProcessor "com.github.bumptech.glide:compiler:$glide_version"
    implementation 'jp.wasabeef:glide-transformations:4.3.0'

    implementation "androidx.media3:media3-exoplayer:$media3_version"
    //implementation "androidx.media3:media3-exoplayer-dash:$media3_version"
    implementation "androidx.media3:media3-ui-leanback:$media3_version"
    implementation "androidx.media3:media3-ui:$media3_version"

    implementation project(path: ':repository:DanmakuFlameMaster')
    implementation 'com.github.ctiao:ndkbitmap-armv7a:0.9.24'
    implementation 'com.github.ctiao:ndkbitmap-armv5:0.9.24'
    implementation 'com.github.ctiao:ndkbitmap-x86:0.9.24'

    implementation "io.reactivex.rxjava3:rxjava:$rxjava3_version"
    implementation "io.reactivex.rxjava3:rxandroid:$rxjava3_android_version"

    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-rxjava3:$room_version"
    testImplementation "androidx.room:room-testing:$room_version"

    implementation 'com.google.zxing:core:3.5.3'

    implementation platform('com.google.firebase:firebase-bom:33.5.0')
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-crashlytics'
}